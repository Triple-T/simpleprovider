apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}
dependencies {
    compile 'com.google.android:android:4.1.1.4'

    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.3-SNAPSHOT'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
}

jar.dependsOn test

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def nexusUrl;
def nexusUsername = ""
def nexusPassword = ""

if (isReleaseBuild()) {
    nexusUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    nexusUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}

File propFile = file('signing.properties');
if (propFile.exists()) {
    propFile.withReader { reader ->
        def props = new Properties()
        props.load(reader)

        if (props.containsKey('NEXUS_USERNAME') && props.containsKey('NEXUS_PASSWORD')) {
            nexusUsername = props['NEXUS_USERNAME']
            nexusPassword = props['NEXUS_PASSWORD']
        }
    }
}

//println 'URL: ' + nexusUrl
//println 'Username: ' + nexusUsername
//println 'Password: ' + nexusPassword

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.triplet'
            artifactId 'simpleprovider'
            version '1.0.0-SNAPSHOT'

            pom.withXml {
                def xml = asNode()

                xml.appendNode('packaging', 'jar')
                xml.appendNode('name', 'SimpleProvider Library')
                xml.appendNode('description', 'Create simple ContentProviders for Android Applications reducing boilerplate code')
                xml.appendNode('url', 'https://github.com/Triple-T/simpleprovider')

                def licenses = xml.appendNode('licenses')
                def license = licenses.appendNode('license');
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def scm = xml.appendNode('scm')
                scm.appendNode('url', 'https://github.com/Triple-T/simpleprovider')
                scm.appendNode('connection', 'scm:git:git@github.com:Triple-T/simpleprovider.git')
                scm.appendNode('developerConnection', 'scm:git:git@github.com:Triple-T/simpleprovider.git')

                def developers = xml.appendNode('developers')
                def developerBH = developers.appendNode('developer');
                developerBH.appendNode('id', 'bhurling')
                developerBH.appendNode('name', 'Bjoern Hurling')

                def developerCB = developers.appendNode('developer');
                developerCB.appendNode('id', 'ChristianBecker')
                developerCB.appendNode('name', 'Christian Becker')
            }

            artifact jar
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUsername
                password nexusPassword
            }
            url nexusUrl
        }
    }
}
