apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.android:android:4.1.1.4'

    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
}

group = GROUP
version = VERSION_NAME

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

jar.dependsOn test

apply from: 'gradle-mvn-push.gradle'

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                pom.project {
                    developers {
                        developer {
                            id 'bhurling'
                            name 'Bjoern Hurling'
                        }
                        developer {
                            id 'ChristianBecker'
                            name 'Christian Becker'
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies = pom.dependencies.findAll { dep ->
                        dep.scope != 'test' && dep.groupId != 'com.google.android'
                    }
                }
            }
        }
    }
}