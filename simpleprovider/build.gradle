buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'nexus'

repositories {
    mavenCentral()
}
dependencies {
    compile 'com.google.android:android:4.1.1.4'

    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.2'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

jar.dependsOn test

group 'com.github.triple'
version '1.0.0-SNAPSHOT'

modifyPom {
    project {
        artifactId 'simpleprovider'

        packaging 'jar'
        name 'SimpleProvider Library'
        description 'Create simple ContentProviders for Android Applications reducing boilerplate code'
        url 'https://github.com/Triple-T/simpleprovider'

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        scm {
            url 'https://github.com/Triple-T/simpleprovider'
            connection 'scm:git:git@github.com:Triple-T/simpleprovider.git'
            developerConnection 'scm:git:git@github.com:Triple-T/simpleprovider.git'
        }

        developers {
            developer {
                id 'bhurling'
                name 'Bjoern Hurling'
            }
            developer {
                id 'ChristianBecker'
                name 'Christian Becker'
            }
        }

        dependencies.removeAll { it.groupId == 'com.google.android' }
    }

}
